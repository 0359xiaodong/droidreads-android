apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    output.outputFile.name
                            .replace("app-", "droidreads-")
                            .replace(".apk", "-${variant.versionName}.apk"))
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        applicationId "pl.pelotasplus.droidreads"

        minSdkVersion 14
        targetSdkVersion 21

        versionCode 4
        versionName "4"

        buildConfigField "String", "GOOGLE_ANALYTICS_KEY", '""'

        buildConfigField "String", "GOODREADS_API_KEY", '""'
        buildConfigField "String", "GOODREADS_API_SECRET", '""'
        buildConfigField "String", "GOODREADS_CALLBACK_URL", '"droidreads://"'
        buildConfigField "String", "GOODREADS_API_ENDPOINT", '"https://www.goodreads.com"'

        buildConfigField "String", "GOOGLEBOOKS_API_ENDPOINT", '"https://www.googleapis.com"'
    }

    signingConfigs {
        debug {
            storeFile rootProject.file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:cardview-v7:21.0.3'
    compile 'com.android.support:recyclerview-v7:21.0.3'

    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.jakewharton:butterknife:6.0.0'

    compile 'com.squareup.retrofit:retrofit:1.9.0'

    compile('org.simpleframework:simple-xml:2.7.1') {
        exclude group: 'stax'
        exclude module: 'xpp3'
    }

    compile('com.squareup.retrofit:converter-simplexml:1.9.0') {
        exclude module: 'simple-xml'
    }

    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.3.0'

    compile 'oauth.signpost:signpost-core:1.2.1.2'

    compile 'com.google.dagger:dagger:2.0-SNAPSHOT'
    apt 'com.google.dagger:dagger-compiler:2.0-SNAPSHOT'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    compile 'com.squareup:otto:1.3.5'

    compile 'com.octo.android.robospice:robospice:1.4.14'
    compile 'com.octo.android.robospice:robospice-retrofit:1.4.14'
    compile 'com.octo.android.robospice:robospice-okhttp:1.4.14'

    compile 'com.google.android.gms:play-services-base:6.5.87'

    compile 'com.squareup.picasso:picasso:2.4.0'

    compile 'org.jetbrains.kotlin:kotlin-stdlib:0.10.195'

    compile 'com.github.ksoichiro:android-observablescrollview:1.5.0'

    compile 'com.joanzapata.android:android-iconify:1.0.9'

    compile 'com.makeramen:roundedimageview:2.0.0'

    compile 'com.android.support:palette-v7:21.0.3'
}
